//--------------------------------------------------------------------------------------------------
//= init_h.hsp
//--------------------------------------------------------------------------------------------------
//	ウィンドウ / バッファ / コンパイルオプションなどの初期化
//--------------------------------------------------------------------------------------------------

	//----------------------------------------------------------------------------------------------
	//= 画面がクリックされた時のラベルを設定
	//----------------------------------------------------------------------------------------------
		onclick gosub *__OnClick

	//----------------------------------------------------------------------------------------------
	//= 終了処理ラベルを設定
	//----------------------------------------------------------------------------------------------
		onexit gosub *__OnExit

	//----------------------------------------------------------------------------------------------
	//= ランダムテーブル初期化
	//----------------------------------------------------------------------------------------------
		randomize

	//----------------------------------------------------------------------------------------------
	//= 設定ファイルをロード
	//----------------------------------------------------------------------------------------------
		//存在しなかった場合初期設定で作成する
		if( ExistB(INI_FILE_NAME) == FALSE ){
			//まずファイルを空で作成
			tmp = ""
			notesel tmp
			notesave ""+INI_FILE_NAME
			//セーブ
			gosub *__DataSave
		}
		//読み込み
		gosub *__DataLoad

	//----------------------------------------------------------------------------------------------
	//= BG画像バッファ初期化
	//----------------------------------------------------------------------------------------------
		buffer SCR_BG
		picload "data/img/bg.gif"

	//----------------------------------------------------------------------------------------------
	//= キャラクターアイコンバッファ初期化
	//----------------------------------------------------------------------------------------------
		buffer SCR_CHARACTER_ICON
		picload "data/img/CharacterIcon.png"

	//----------------------------------------------------------------------------------------------
	//= ネームプレート画像バッファ初期化
	//----------------------------------------------------------------------------------------------
		buffer SCR_NAME_PLATE_LEFT	: picload "data/img/NamePlateLeft.gif"
		buffer SCR_NAME_PLATE_FILL	: picload "data/img/NamePlateFill.gif"
		buffer SCR_NAME_PLATE_RIGHT	: picload "data/img/NamePlateRight.gif"

	//----------------------------------------------------------------------------------------------
	//= マウス当たり判定用のバッファ初期化
	//----------------------------------------------------------------------------------------------
		buffer SCR_MOUSE_COLLISION, WINDOW_SIZE_W, WINDOW_SIZE_H
		cls 4

	//----------------------------------------------------------------------------------------------
	//= キャラクターセレクトウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		screen SCR_CHARACTER_SELECT, CHARACTER_SELECT_WINDOW_SIZE_W, CHARACTER_SELECT_WINDOW_SIZE_H, 2+4+8
		title "キャラクター変更"
		InitCharacterSelectWindow
		//オンコマンド指定
		oncmd gosub *__WM_ACTIVATE, WM_ACTIVATE

	//----------------------------------------------------------------------------------------------
	//= 下書きウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		buffer SCR_DRAFT, WINDOW_SIZE_W, WINDOW_SIZE_H, screen_fixedsize

	//----------------------------------------------------------------------------------------------
	//= メインウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		screen SCR_MAIN, WINDOW_SIZE_W, WINDOW_SIZE_H+GetMenubarHeight(), screen_fixedsize, WindowPositionX, WindowPositionY
		title ""+PROGRAM_NAME+" "+PROGRAM_VERSION
		gosub *__CreateMenubar
		//常に最前面に表示を適用
		if( AlwaysOnTopFlag == TRUE ){
			gsel SCR_MAIN, 2
		}
		//コンパクトモードを適用
		if( CompactModeFlag == TRUE ){
			width COMPACTMODE_SIZE_W, COMPACTMODE_SIZE_H+GetMenubarHeight()
		}
		//ウィンドウが画面からはみ出してる場合に調整する
		x = ginfo_winx
		y = ginfo_winy+GetMenubarHeight()
		if( ginfo_wx1 <= -1 ){
			width x, y, 0
		}
		if( ginfo_wx2 >= ginfo_dispx ){
			width x, y, ginfo_dispx-ginfo_sizex
		}
		if( ginfo_wy1 <= -1 ){
			width x, y, , 0
		}
		if( ginfo_wy2 >= ginfo_dispy ){
			width x, y, , ginfo_dispy-ginfo_sizey
		}

		//オンコマンド指定
		oncmd gosub *__WM_COMMAND, WM_COMMAND