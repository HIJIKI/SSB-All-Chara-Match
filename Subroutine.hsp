//--------------------------------------------------------------------------------------------------
//= subroutine.hsp
//--------------------------------------------------------------------------------------------------
//	各種サブルーチン
//--------------------------------------------------------------------------------------------------

	//----------------------------------------------------------------------------------------------
	//= ×ボタンが押された時
	//----------------------------------------------------------------------------------------------
		*__End
			//メインウィンドウ
			if( wparam == SCR_MAIN ){
				//多重表示防止
				if( ExitCheckDialog == FALSE ){
					ExitCheckDialog = TRUE
					dialog "終了しますか？", 2, ""+PROGRAM_NAME+" "+PROGRAM_VERSION
					if(stat == 6){
						end
					}
					ExitCheckDialog = FALSE
				}
			}
			//キャラクターセレクトウィンドウ
			if( wparam == SCR_CHARACTER_SELECT ){
				GselMem = ginfo_sel
				gsel wparam, -1
				gsel GselMem
			}
		return

	//----------------------------------------------------------------------------------------------
	//= 画面がクリックされた時
	//----------------------------------------------------------------------------------------------
		*__OnClick
			stick Key, 256+512
			//メインウィンドウ
			if( ginfo_act == SCR_MAIN ){
				//左クリック
				if Key = 256 {
					if( mousex < WINDOW_SIZE_W )&&( mousey < WINDOW_SIZE_H ){
						GetPixelOnID SCR_MOUSE_COLLISION, mousex, mousey
						result = ginfo_r
						//モード切り替え
						if( result == 128 ){
							GameModeFlag ^= 1
							//入力欄 有効/無効化
							objenable PlayerNameInputObjectID(2), GameModeFlag
							objenable PlayerNameInputObjectID(3), GameModeFlag
							//再描画
							DrawAllGraphic
						}
						//キャラアイコンを切り替え
						if( result == 255 ){
							player = ginfo_g
							id = ginfo_b
							CharacterFlag(player, id) ^= 1
							//再描画
							DrawAllGraphic
						}
					}
				}
				//右クリック
				if Key = 512 {
					if( mousex < WINDOW_SIZE_W )&&( mousey < WINDOW_SIZE_H ){
						GetPixelOnID SCR_MOUSE_COLLISION, mousex, mousey
						result = ginfo_r
						//キャラ変更ウィンドウを表示
						if( result == 255 ){
							player = ginfo_g
							id = ginfo_b
							GselMem = ginfo_sel
							gsel SCR_CHARACTER_SELECT, 2
							width , , ginfo_mx, ginfo_my
							gsel GselMem
						}
					}
				}
			}
			//キャラクターセレクトウィンドウ
			if( ginfo_act == SCR_CHARACTER_SELECT ){
				//左クリック
				if Key = 256 {
					GselMem = ginfo_sel
					gsel SCR_CHARACTER_SELECT
					//有効範囲内であるか
					if( mousex < CHARACTER_ICON_SIZE_W*3 )&&( mousey < CHARACTER_ICON_SIZE_H*4 ){
						//マウス座標から選択キャラを算出
						x = mousex/CHARACTER_ICON_SIZE_W: y = (mousey/CHARACTER_ICON_SIZE_H)*3
						chara = x+y
						//キャラ変更を反映
						CharacterSet(player, id) = chara
						//反映
						gsel SCR_CHARACTER_SELECT, -1
						gsel GselMem
						DrawAllGraphic
					}
				}
			} else {
				//アクティブでなくなった場合非表示に
				GselMem = ginfo_sel
				gsel SCR_CHARACTER_SELECT, -1
				gsel GselMem
			}
		return

	//----------------------------------------------------------------------------------------------
	//= オブジェクト初期化
	//----------------------------------------------------------------------------------------------
		*__ObjectInit
			baseX = OBJECT_BOX_POS_X
			baseY = OBJECT_BOX_POS_Y
			s = 4	//セパレータ

			//プレイヤーネーム入力欄
			w = OBJECT_BOX_SIZE_W/PLAYER_MAX-s*2
			h = 24
			x = baseX+s*2
			y = baseY+(OBJECT_BOX_SIZE_H/2)-(128/2)+h
			objsize w, h
			repeat PLAYER_MAX
				pos x+(w+s)*cnt, y
				input PlayerName(cnt)
				PlayerNameInputObjectID(cnt) = stat
				PlayerNameInputObjectHandle(cnt) = objinfo(stat, 2)
			loop
			objenable PlayerNameInputObjectID(2), GameModeFlag
			objenable PlayerNameInputObjectID(3), GameModeFlag

			//キャラランダム
			x = (baseX+OBJECT_BOX_SIZE_W/2)-360/2
			y = ginfo_cy+s*4
			;input
			pos x+0, y
			objsize 40, 24
			CharacterRandomNumber = CHARACTER_NUMBER
			input CharacterRandomNumber
			CharacterRandomNumberInputObjectID = stat
			CharacterRandomNumberInputObjectHandle = objinfo(stat, 2)
			winobj "msctls_updown32", "", , $50000012 | UDS_ALIGNRIGHT
			hUpdown = objinfo(stat, 2)
			sendmsg hUpdown, $465, , MAKELONG(12, 1)	;最小/最大値指定
			;button
			pos x+40+s, y
			objsize 128, 24
			button gosub "キャラランダム", *__CharacterRandomSelect
			;chkbox
			pos x+40+s+128+s, y
			objsize 192, 24
			chkbox "キャラかぶりなし(6キャラ以下の時)", NotSameCharacterFlag
			NotSameCharacterCheckboxObjectID = stat
			;イベントが起きるため後回し
			objprm CharacterRandomNumberInputObjectID, CHARACTER_NUMBER

			//クリップボード
			x = (baseX+OBJECT_BOX_SIZE_W/2)-144/2
			y = ginfo_cy+s*4
			pos x, y
			objsize 144, 24
			button gosub "クリップボードにコピー", *__SetClipBoard
		return

	//----------------------------------------------------------------------------------------------
	//= キャラランダム
	//----------------------------------------------------------------------------------------------
		*__CharacterRandomSelect
			//キャラセットが編集されている場合正常に動作しない旨を表示
			flag = FALSE
			for i, 0, 2, 1
				for j, 0, CHARACTER_NUMBER, 1
					if( CharacterSet(i, j) != j ){
						flag = TRUE
					}
				next
			next
			if( flag == TRUE )&&( CharacterRandomCautionFlag == FALSE ){
				dialog "キャラクターランダム機能は\nユーザーにより変更されたキャラセットを考慮していません。\nこのまま処理を続けますか？", 3, "キャラクターセットが変更されています"
				if( stat = 7 ){
					return
				}
				CharacterRandomCautionFlag = TRUE
			}
			//不正な数値の場合はリストア
			CharacterRandomNumber = limit(CharacterRandomNumber, 1, CHARACTER_NUMBER)
			objprm CharacterRandomNumberInputObjectID, CharacterRandomNumber
			//ランダム開始
			if CharacterRandomNumber == CHARACTER_NUMBER {
				dim CharacterFlag, 2, CHARACTER_NUMBER
			} else {
				//リセット
				for i, 0, 2
					for j, 0, CHARACTER_NUMBER
						CharacterFlag(i, j) = TRUE
					next
				next
				//1pランダム
				i = 0
				repeat
					tmp = rnd(CHARACTER_NUMBER)
					if( CharacterFlag(0, tmp) == TRUE ){
						CharacterFlag(0, tmp) == FALSE
						i++
					}
					if( i == CharacterRandomNumber ){
						break
					}
				loop
				//2pランダム
				i = 0
				repeat
					tmp = rnd(CHARACTER_NUMBER)
					f = FALSE
					if( CharacterFlag(1, tmp) == TRUE ){
						//キャラかぶりなしかどうか
						if( CharacterRandomNumber <= 6 )&&( NotSameCharacterFlag == TRUE ){
							if( CharacterFlag(0, tmp) == TRUE ){
								f = TRUE
							}
						} else {
							f = TRUE
						}
					}
					if( f == TRUE ){
						CharacterFlag(1, tmp) == FALSE
						i++
					}
					if( i == CharacterRandomNumber ){
						break
					}
				loop
			}
			//再描画
			DrawAllGraphic
		return

	//----------------------------------------------------------------------------------------------
	//= クリップボードにコピー
	//----------------------------------------------------------------------------------------------
		*__SetClipBoard
			//データ整形
			put = ""
			p = 1
			for i, 0, 2
				if( GameModeFlag == 0 ){
					put += "【"+p+"P】"
					p++
				} else {
					put += "【"+p+"P&"+(p+1)+"P】"
					p += 2
				}
				for j, 0, CHARACTER_NUMBER
					if( CharacterSet(i, j) != -1 ){
						if( CharacterFlag(i, j) == FALSE ){
							put += " "+CharacterName(CharacterSet(i, j))
						}
					}
				next
				if( i == 0 ){ put += "\n" }
			next
			//テキスト描画
			color 255,255,255
			pos WINDOW_SIZE_W/2, WINDOW_SIZE_H-NAME_PLATE_SIZE_H-64
			mes "コピーしました。"
			//クリップボードにセット
			clipset ""+put
		return

	//----------------------------------------------------------------------------------------------
	//= メニューバーを作成
	//----------------------------------------------------------------------------------------------
		*__CreateMenubar
			//[ファイル]内項目
			newmenu SubmenuHandle(0), 1
			addmenu SubmenuHandle(0), "キャラセットを初期化(&D)", MENU_INITCHARASET
			addmenu SubmenuHandle(0), "キャラセットを開く(&O)", MENU_OPENCHARASET
			addmenu SubmenuHandle(0), "キャラセットを保存(&S)", MENU_SAVECHARASET

			//[オプション]内項目
			newmenu SubmenuHandle(1), 1
			;常に最前面に表示
			if( AlwaysOnTopFlag == TRUE ){
				addmenu SubmenuHandle(1), "常に最前面に表示", MENU_ALWAYSONTOP, 0x8
			} else {
				addmenu SubmenuHandle(1), "常に最前面に表示", MENU_ALWAYSONTOP
			}

			//[ヘルプ]内項目
			newmenu SubmenuHandle(2), 1
			addmenu SubmenuHandle(2), ""+PROGRAM_NAME+" について", MENU_ABOUT

			//メニューバー
			newmenu MenubarHandle, 0
			addmenu MenubarHandle, "ファイル(&F)", SubmenuHandle(0), $10
			addmenu MenubarHandle, "オプション(&O)", SubmenuHandle(1), $10
			addmenu MenubarHandle, "ヘルプ(&H)", SubmenuHandle(2), $10

			//メニューバーをウィンドウに反映
			applymenu MenubarHandle
		return

	//----------------------------------------------------------------------------------------------
	//= オンコマンド
	//----------------------------------------------------------------------------------------------
		*__OnCommand
			//メニューバーが操作された
			if( lparam == 0 ){
				id = LOWORD(wparam)
				//キャラセットを初期化
				if( id == MENU_INITCHARASET ){
					gosub *__InitCharacterSet
					//再描画
					DrawAllGraphic
				}
				//キャラセットを開く
				if( id == MENU_OPENCHARASET ){
					gosub *__OpenCharacterSet
				}
				//キャラセットを保存
				if( id == MENU_SAVECHARASET ){
					gosub *__SaveCharacterSet
				}
				//終了
				if( id == MENU_EXIT ){
					gosub *__End
				}
				//常に最前面に表示
				if( id == MENU_ALWAYSONTOP ){
					AlwaysOnTopFlag ^= 1
					if( AlwaysOnTopFlag == TRUE ){
						gsel SCR_MAIN, 2
					} else {
						gsel SCR_MAIN, 1
					}
					//チェックマークを更新するためメニューバー項目を再構築
					gosub *__CreateMenubar
				}
				//バージョン情報
				if( id == MENU_ABOUT ){
					gosub *__Version
				}
				return
			}

			//名前が変更された
			dupptr hnd, varptr(PlayerNameInputObjectHandle), 4*4, 4
			if( lparam == hnd(0) )||( lparam == hnd(1) )||( lparam == hnd(2) )||( lparam == hnd(3) ){
				if( HIWORD(wParam) == EN_CHANGE ){
					DrawAllGraphic
				}
				return
			}

			//ランダムキャラ数が変更された
			if( lparam == CharacterRandomNumberInputObjectHandle ){
				if( HIWORD(wParam) == EN_CHANGE ){
					if( CharacterRandomNumber > 6 ){
						objenable NotSameCharacterCheckboxObjectID, 0
					} else {
						objenable NotSameCharacterCheckboxObjectID, 1
					}
				}
				return
			}

		return

	//----------------------------------------------------------------------------------------------
	//= キャラセットを初期化
	//----------------------------------------------------------------------------------------------
		*__InitCharacterSet
			for i, 0, 2, 1
				for j, 0, CHARACTER_NUMBER, 1
					CharacterSet(i, j) = j
				next
			next
		return

	//----------------------------------------------------------------------------------------------
	//= キャラセットを開く
	//----------------------------------------------------------------------------------------------
		*__OpenCharacterSet
			dialog "sacmset", 16, "キャラセットファイル"
			if( stat == 1 ){
				bload ""+refstr, CharacterSet
			}
			//再描画
			DrawAllGraphic
		return

	//----------------------------------------------------------------------------------------------
	//= キャラセットを保存
	//----------------------------------------------------------------------------------------------
		*__SaveCharacterSet
			dialog "sacmset", 17, "キャラセットファイル"
			filename = getpath(refstr, 1)
			if( stat == 1 ){
				bsave ""+filename+".sacmset", CharacterSet
			}
		return

	//----------------------------------------------------------------------------------------------
	//= バージョン情報
	//----------------------------------------------------------------------------------------------
		*__Version
			dialog ""+PROGRAM_NAME+"\nVersion "+PROGRAM_VERSION+"\n\n(C)2012 HIJIKI, HIJIKI SOFT"+"\n\nBuild "+__date__+" "+__time__, , ""+PROGRAM_NAME+" について"
		return